COMPILER Ela

CHARACTERS
	cr = '\r'.
	lf = '\n'.
	tab = '\t'.    
	letter = 'A' .. 'Z' + 'a' .. 'z' + '_'.
	ucase = 'A' .. 'Z'.
	lcase = 'a' .. 'z'.
	digit = "0123456789".
	notDigit = ANY - digit.
	hexDigit = digit + "ABCDEFabcdef".
	verbatimChar = ANY.
	stringChar = ANY - '"' - '\\' - cr - lf.
	char = ANY - "'" - '\\' - cr - lf.
	op = "!%&+-.,:<=>?@^|$~`\\".
	parens = "[]{}()".

TOKENS
    ident = lcase { letter | digit | "'" }
			| "_" ("'"|letter) { letter | digit | "'" }
			| "'" letter [ (letter|digit|"'") {letter|digit|"'"} (letter|digit) ].	
	
	variantTok = ucase { letter | digit | "'" }.
	
	intTok = ["-"]( digit { digit } 
	          | digit { digit } CONTEXT ("." notDigit) 
	          | ("0x"|"0X") hexDigit { hexDigit } 
	          ) ["L"|"l"].
			
	realTok = ["-"]"." digit { digit } [ ("e"|"E") ["+"|"-"] digit { digit } ] ["D"|"d"|"F"|"f"] 
			| ["-"] digit { digit } 
			    ( 
				    "." digit { digit } [ ( "e" | "E" ) ["+" | "-" ] digit { digit } ] ["D"|"d"|"F"|"f"] 
				    | ("e"|"E") ["+"|"-"] digit { digit } ["D"|"d"|"F"|"f"]  
				    | "D"|"d"|"F"|"f"|"M"|"m"
			    ).			
			
	stringTok = "\"" { stringChar
					| "\\\"" | "\\\\" | "\\0" | "\\b" | "\\n" | "\\r" | "\\t" | "\\'"
					| "\\u" hexDigit hexDigit hexDigit hexDigit
					}  
				"\"".				
	
	charTok = 	"'" ( char
					| "\\\"" | "\\\\" | "\\0" | "\\b" | "\\n" | "\\r" | "\\t" | "\\'"
					| "\\u" hexDigit hexDigit hexDigit hexDigit
					)  
				"'".
	operatorTok = op { op }.
	
	parenTok = parens { parens }.
	
	NL = cr|lf.
	
IGNORE tab

PRODUCTIONS
	Comment =
		(
			"/*" 
			(. var pos = t.pos; .)
			{ ANY } ("*/")
			(. 
				Add(pos, t.pos - pos + t.val.Length, ElaStyle.MultilineComment); 
			.)
		).
				
	SingleLineComment = 
		(
			"//"  (. var pos = t.pos; .) 
			{ ANY } 
			(
			NL (. Add(pos, t.pos - pos + t.val.Length - 1, ElaStyle.Comment); .)
			|
			EOF (. Add(pos, t.pos - pos + t.val.Length, ElaStyle.Comment); .)
			)
			
		).
		
	VerbatimString = 
	    "<[" (. var pos = t.pos; .) {ANY} ("]>")
	    (. Add(pos, t.pos - pos + t.val.Length, ElaStyle.String); .).
				
	Primary = 
		(
			ident (. Add(t.pos, t.val.Length, ElaStyle.Identifier); .)
			| variantTok (. Add(t.pos, t.val.Length, ElaStyle.Variant); .)
			| intTok (. Add(t.pos, t.val.Length, ElaStyle.Number); .)
			| realTok (. Add(t.pos, t.val.Length, ElaStyle.Number); .)
			| stringTok (. Add(t.pos, t.val.Length, ElaStyle.String); .)
			| charTok (. Add(t.pos, t.val.Length, ElaStyle.Char); .)
			| (operatorTok|"*"|"/"|"*/") (. Add(t.pos, t.val.Length, ElaStyle.Operator); .)
			| "true" (. Add(t.pos, t.val.Length, ElaStyle.Literal); .)
			| "false" (. Add(t.pos, t.val.Length, ElaStyle.Literal); .)
			| parenTok
			| "_"
			| ";" | "#"
		).
		
	Keywords = 
		( "let" | "do" | "where" | "open" | "import" | "in" | "is" | "if" | "then" | "else" | "match" | "try" | "with" | "__internal" | "deriving" | "fail" | "instance" | "class" | "type" | "data" | "opentype" )
		(. Add(t.pos, t.val.Length, ElaStyle.Keyword); .).
		
	Code = Keywords | VerbatimString | Comment | SingleLineComment | Primary | NL.// | Thunk.
	
	Ela = Code { Code }.

END Ela.