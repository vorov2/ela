/* 
  Module string
  Created in 2012-2016 by Basil Voronkov
*/
import libString#elalib@str
import lang

/* Classes and instances */
class Format a where
  fmt _->a->_

instance Format Int Long Single Double String Char Bool where
  fmt f x = lang.toString (force f) x

instance ListLike String where 
  toList x    = str.toList x
  fromList xs = toString <| trav "" xs
    where trav sb (x::xs) = trav (sb +> toString x) xs
          trav sb [] = sb

/* Functions */
format = formatfn id

formatfn fn !fstr
  | isError fmtRes = fmtRes
  | else = let (fstr2,fsf) = fmtRes in format fstr2 [] fsf
  where fmtRes = str.getFormats fstr
        format fstr2 xs [] = fn $ str.format fstr2 xs
        format fstr2 xs (f::fms) = walk xs f
            where
                walk xs [] _ = format fstr2 xs fms
                walk xs (fa::fas) x = walk (fmt fa x :: xs) fas x

upper # qualified
upper x = str.upper (force x)

lower # qualified
lower x = str.lower (force x)

trim x = str.trim (force x)
trimChars = collect str.trimChars []
trimStart s = str.trimStart (force s)
trimStartChars = collect str.trimStartChars []
trimEnd s = str.trimEnd (force s)
trimEndChars = collect str.trimEndChars []

indexOf search s = str.indexOf (force search) (force s)
indexOfFrom search idx s = str.indexOfFrom (force search) (force idx) (force s)
indexOfLast search s = str.indexOfLast (force search) (force s)

startsWith search s = str.startsWith (force search) (force s)
endsWith search s = str.endsWith (force search) (force s)

replace search repl s = str.replace (force search) (force repl) (force s)

remove start n s = str.remove (force start) (force n) (force s)

substr start n s = str.substr (force start) (force n) (force s)

split !sep !s = str.split sep s

insert !idx !ins !s = str.insert idx ins s

join sep (x::xs) = _join (toString x) xs
  where _join _ [] = ""
        _join str (x::[]) = toString $ str +> sep +> x
        _join str (x::xs) = _join (str +> sep +> x) xs

unwords = join " "

words = split " "

unlines = join "\n"

lines = split "\n" << replace "\n\n" "\n" << replace "\r" "\n"

padLeft_ _ 0 str = str
padLeft_ sym num str = padLeft_ sym (num - 1) (sym ++ str)

padLeft = padLeft_ " "

padRight_ _ 0 str = str
padRight_ sym num str = padRight_ sym (num - 1) (str ++ sym)

padRight = padRight_ " "

//This function is used to collect chars in a list
collect # private
collect fn xs x 
  | x is Char = collect fn (x::xs)
  | else = fn xs x

