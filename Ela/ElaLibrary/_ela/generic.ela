/*
  Module data
  Created by Basil Voronkov in 2012
*/
/* Reflection API */
//Constructor name: Alg->String
consName          = __internal api1

//Constructor parameter number: Alg->Int
consParamNumber   = __internal api2

//Constructor index: Alg->Int
consIndex         = __internal api3

//Constructor is infix symbol: Alg->Bool
consInfix         = __internal api7

//Constructor unique ID: Alg->Int
consCode          = __internal api9

//Constructor unique ID by index: Int->Int->Int
consCodeByIndex   = __internal api104

//Type name: (any)->String
typeName          = __internal api4

//Type unique ID: (any)-Int
typeCode          = __internal api5

//Number of constructor in type: Int->Int
typeConsNumber    = __internal api6

//Index of a parameter in constructor: String->Int->Int
consParamIndex    = __internal api101

//Value of a parameter in constructor: Int->Alg->(any)
consParamValue    = __internal api102

//Name of a parameter: Int->Int->String
consParamName     = __internal api103

//Checks is a cons parameter exists: String->Int->Bool
consParamExist    = __internal api105

//Creates a default instance: Int->Alg
consCreateDefault = __internal api14

//Create a production: Int->Fun or Int->Alg
consCreate        = __internal api15

//An index of a constructor by name: Int->String->Int
consNameIndex     = __internal api107

//Returns try if a value is AlgDT: Alg->Bool
isAlgebraic       = __internal api8

/* Default instances */
//Only for algebraic, open types are welcome
instance Show where
  show a 
    | consInfix a && len == 2 = showAlg (consParamValue 0 a) ++ " " 
                                ++ consName a ++ " " ++ showAlg (consParamValue 1 a)
    | else = toString <| show' (consName a) 0
    where len = consParamNumber a
          showAlg x 
            | isAlgebraic x && consParamNumber x > 0 = "(" ++ show x ++ ")"
            | else = show x
          show' sb n 
            | n < len = show' (sb +> " " +> showAlg (consParamValue n a)) (n+1)
            | else = sb

//Only for algebraic, open types are welcome
instance Eq where
  a == b = consCode a == consCode b && cyc (consParamNumber a)
    where cyc 0 = true
          cyc n 
            | consParamValue (n - 1) a == consParamValue (n - 1) b = cyc (n - 1)
            | else = false

//Only for algebraic, bettern not to use with open types
instance Ord where
  compare a b
    | consIndex a > consIndex b = GT
    | consIndex a < consIndex b = LT
    | else = cyc (consParamNumber a)
    where cyc 0 = EQ
          cyc n 
            | av == bv = cyc (n - 1)
            | av > bv = GT
            | else = LT
            where av = consParamValue (n - 1) a
                  bv = consParamValue (n - 1) b

//Only for algebraic, open types are welcome
instance Name where
  getField nm xs = consParamValue (consParamIndex nm (consCode xs)) xs
  isField fld a = consParamExist fld (consCode a)

//Only for algebraic, open types are welcome
instance Len where
  length = consParamNumber

//Only for algebraic, open types are welcome
instance Ix where
  getValue = consParamValue

//Only for algebraic, better not to use with open types
instance Enum where
  succ a = consCreateDefault (consCodeByIndex (typeCode a) (consIndex a + 1))
  pred a = consCreateDefault (consCodeByIndex (typeCode a) (consIndex a - 1))
  enumFrom fst snd = enumFromTo <| typeConsNumber (typeCode fst)
  enumFromTo max fst snd = map (\x -> consCreateDefault (consCodeByIndex tc x)) 
      [consIndex fst,consIndex snd..consIndex max]
    where tc = typeCode fst

//Only for algebraic, better not to use with open types
instance Bounded where
  maxBound = __internal genmaxbound
  minBound = __internal genminbound

//Only for algebraic, recommended for types where first constructor
//has not parameters.
instance Default where
  default = __internal gendefault

//Only for algebraic, better not to use with open types
instance IntLike where
  toInt = consIndex
  fromInt x = consCreateDefault <| consCodeByIndex context x

//Only for algebraic, better not to use with open types
instance LongLike where
  toLong = toLong << consIndex
  fromLong x = fromInt (toInt x)

//Only for algebraic, open types are welcome
instance StringLike where
  toString = consName
  fromString x = consCreateDefault <| consCodeByIndex context (consNameIndex context x)

//(Partial) Only for algebraic, open types are welcome
instance TupleLike where
  toTuple a
    | len == 0 = fail "TupleLike.toTuple: Type constructor has no parameters."
    | len == 1 = (consParamValue 0 a,)
    | len == 2 = (consParamValue 0 a,consParamValue 1 a)
    | len == 3 = (consParamValue 0 a,consParamValue 1 a,consParamValue 2 a)
    | else = cyc 0
    where
      len = consParamNumber a
      cyc n | n < len - 1 = (consParamValue n a,) ++ cyc (n+1)
            | else = (consParamValue n a,)

//(Partial) Only for algebraic, open types are welcome
instance ListLike where
  toList a
    | len == 0 = []
    | else = tol 0
    where
      len = consParamNumber a
      tol n 
        | n < len = consParamValue n a :: tol (n+1)
        | else = []
